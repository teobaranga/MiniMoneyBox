apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

repositories {
    google()
}

// local.properties used to store information that should not be checked into VCS
def localProperties = new Properties().with { prop ->
    new File("local.properties").withInputStream {
        prop.load(it)
    }
    return prop
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.example.minimoneybox"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // MoneyBox AppId
        buildConfigField 'String', "APP_ID", "\"${localProperties["moneybox.appId"]}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    def ver = [
            koin: '2.0.1',
            moshi: '1.9.2',
            retrofit: '2.6.2',
            room: '2.2.2',
    ]

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta3'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0-rc03"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0-rc03"
    implementation "androidx.room:room-runtime:${ver.room}"
    implementation "androidx.room:room-rxjava2:${ver.room}"
    implementation 'com.airbnb.android:lottie:3.3.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation "org.koin:koin-android:${ver.koin}"
    implementation "org.koin:koin-android-viewmodel:${ver.koin}"
    implementation "com.squareup.moshi:moshi-kotlin:${ver.moshi}"
    implementation "com.squareup.okhttp3:logging-interceptor:4.2.2"
    implementation "com.squareup.retrofit2:retrofit:${ver.retrofit}"
    implementation "com.squareup.retrofit2:converter-moshi:${ver.retrofit}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${ver.retrofit}"
    implementation "io.reactivex.rxjava2:rxjava:2.2.15"

    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "io.mockk:mockk:1.9.3"
    testImplementation 'junit:junit:4.12'
    testImplementation "org.koin:koin-test:${ver.koin}"

    androidTestImplementation('androidx.test:runner:1.1.0') {
        exclude module: 'support-annotations'
    }
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0') {
        exclude module: 'support-annotations'
    }

    kapt "androidx.room:room-compiler:${ver.room}"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:${ver.moshi}"
}
